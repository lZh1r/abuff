/// Prints provided arguments to the standard output.
/// If more than 1 argument is provided, they will be separated by commas:
/// ```
/// print(1,2,3);
/// // output: 1, 2, 3
/// ```
///
/// Any value can be printed using this function:
/// ```
/// let a = {
///     b: 2
/// };
/// print(a);
/// // output: { b: 2 }
/// ```
/// 
/// In case of all functions and closures a string "closure" will be printed:
/// ```
/// fun f() {1;};
/// print(f);
/// // output: closure
/// ```
///
/// Printing native functions will give more information
/// in form of this function's name and its module path:
/// ```
/// print(print);
/// // output: native function "print" from "std/builtins"
/// ```
export native fun print(...args: Any[]);

/// Returns program's uptime in nanoseconds.
/// Can be used for benchmarking performance:
/// ```
/// let start = clock();
/// // run some heavy function
/// heavy();
/// let end = clock();
/// print(end - start);
/// ```
export native fun clock(): Int;

/// Attempts to read contents of a file into a string.
export native fun readfile(path: String): Result<String>;

/// Attempts to write content to a file.
/// On success the file's content will be overwritten with provided content.
/// If the file does not exist it will get created.
export native fun writefile(path: String, content: String): Result<Void>;

/// Asserts that an expression is true, otherwise panics
/// ```
/// let a = 9;
/// let b = 2;
/// assert(a % b == 0, "Oopsie!") // panics and prints "Oopsie"
/// ```
export fun assert(expression: Bool, reason: String) {
    if (!expression) panic reason
}

/// Enum representing optional values.
/// Used where a value may or may not be present.
/// Can be unwrapped via pattern matching or methods (wip).
export enum Option<T> {
    Some: T,
    None
} impl {
    /// Converts this Option object into a Result object.
    /// Some turns into Ok
    /// None turns into Err
    fun intoResult(): Result<T> {
        match self {
            .Some(value) -> Result.Ok(value),
            .None(_) -> Result.Err({message: "the value is not provided"})
        }
    }
    
    fun isSome(): Bool {
        match self {
            .Some(_) -> true,
            .None(_) -> false
        }
    }
    
    fun isNone(): Bool {
        match self {
            .Some(_) -> false,
            .None(_) -> true
        }
    }
    
    /// Assumes that the Option object cotains some value and returns it; otherwise, panics.
    fun unwrap(): T {
        match self {
            .Some(value) -> value,
            .None(_) -> panic "Called unwrap on None"
        }
    }
};

/// Constructs an Option.Some of type T.
/// Used as a shorthand for Option.Some().
export fun Some<T>(value: T): Option<T> {
    Option.Some(value)
}

/// Constructs an Option.None of type Any.
/// Used as a shorthand for Option.None().
export fun None<T>(): Option<T> {
    Option.None()
}

/// The default type for errors.
export type Error = {
    message: String
};

export enum Result<T> {
    Ok: T,
    Err: Error
} impl {
    /// Converts this Result object into an Option object.
    /// Ok turns into Some
    /// Err turns into None
    fun intoOption(): Option<T> {
        match self {
            .Ok(value) -> Option.Some(value),
            .Err(_) -> Option.None()
        }
    }

    fun isOk(): Bool {
        match self {
            .Ok(_) -> true,
            .Err(_) -> false
        }
    }
    
    fun isErr(): Bool {
        match self {
            .Ok(_) -> false,
            .Err(_) -> true
        }
    }
    
    /// Assumes that the Result object is Ok and returns its value; panics if it contains an error.
    fun unwrap(): T {
        match self {
            .Ok(value) -> value,
            .Err(_) -> panic "Called unwrap on Err"
        }
    }
};

native type Array<T> impl {
    /// Returns a sorted copy of an array.
    /// Uses Rust's slice.sort_by() which has O(n * log(n)) time complexity.
    native fun sort(): T[];
    
    /// Returns a reversed copy of an array.
    native fun reverse(): T[];
    
    /// Returns the number of elements in an array.
    native fun len(): Int;
    
    /// Returns true if an array is empty.
    fun isEmpty(): Bool {
        self.len() == 0
    }
    
    /// Returns a value at index if it is within the array's boundaries;
    /// otherwise, returns None
    native fun get(index: Int): Option<T>;
};

native type String impl {
    /// Returns an Option contatining a char if the provided index is within the string;
    /// otherwise, returns None.
    native fun get(index: Int): Option<Char>;
    
    /// Returns a reversed copy of a string.
    native fun reverse(): String;
    
    /// Returns the length of an underlying char slice.
    native fun len(): Int;
    
    /// Returns true if a string is empty (length = 0).
    fun isEmpty(): Bool {
        self.len() == 0
    }
    
    //TODO: allow pattern to be a char.
    native fun startsWith(pattern: String): Bool;
    
    //TODO: allow pattern to be a char.
    native fun endsWith(pattern: String): Bool;
    
    /// Returns an upper case copy of a string.
    native fun toUpperCase(): String;
    
    /// Returns a lower case copy of a string.
    native fun toLowerCase(): String;
    
    /// Returns a copy of a string with all leading and trailing whitespaces removed.
    native fun trim(): String;
};